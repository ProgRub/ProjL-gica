/*Projeto LÃ³gica Computacional*/

/*
Elementos do grupo:
Diego AndrÃ©s da Silva BriceÃ±o (2043818),
Filipe Orlando Namora Gomes (2045218),
JosÃ© Alejandro Ferreira Gouveia (2028616),
RÃºben JosÃ© Gouveia Rodrigues (2046018)
*/


/* Predicados auxiliares (conetivos)*/
:-op(100, fy, 'neg').
:-op(200, xfy, 'e').
:-op(300, xfy, 'ou').
:-op(400, xfy, 'imp').


/*acrescenta/3 Ã© tal que acrescenta(X,L1,L2) tem o valor verdadeiro se L2 Ã© a lista que  resulta de colocar o elemento X na cabeÃ§a da lista L1*/
acrescenta(X,[],[X]).
acrescenta(X,L,[X|L]).

/*concatena/3 Ã© tal que concatena(L1,L2,L3) Ã© verdadeiro se L3 Ã© a lista que resulta de juntar as listas L1 e L2*/
concatena([],L,L).
concatena([X|R],L,[X|S]) :- concatena(R,L,S).

/* membro/2 Ã© tal que membro(X,L) tem o valor verdadeiro se X pertence Ã  lista L*/
membro(X, [X | _]).
membro(X, [_ | C]) :- membro(X, C).


/* comprimento/2 Ã© tal que comprimento(L,N) Ã© verdadeiro se N Ã© o comprimento da lista L*/
comprimento([], 0).
comprimento([_|X], N) :-comprimento(X, N1), N is N1+1.

/*eliminarep/2 Ã© tal que eliminarep(X,Y) Ã© verdadeiro se Y Ã© a lista que resulta de retirar da lista X todos exceto a ultima ocorrencia de cada um dos seus elementos*/
eliminarep([],[]).
eliminarep([X|R],S) :- membro(X,R), eliminarep(R,S).
eliminarep([X|R],[X|S]) :- not(membro(X,R)), eliminarep(R,S).


/* enesimo/3 Ã© tal que enesimo(N,L,X) Ã© verdadeiro se N Ã© um nÃºmero natural positivo e X Ã© o elemento que estÃ¡ na posiÃ§Ã£o N da lista L*/
enesimo(1,[X|_],X).
enesimo(N,[_|L],Y) :- enesimo(N1,L,Y), N is N1+1.


/* calc_valor/4 Ã© tal que ao executar uma consulta do tipo calc_valor(F,S,L,V) onde F Ã© uma fÃ³rmula da Linguagem neg, e, ou e imp, S Ã© uma lista, sem repetiÃ§Ãµes, dos sÃ­mbolos proposicionais que ocorrem
em dita fÃ³rmula e L uma lista de 0s e 1s com o mesmo comprimento da lista S, o interpretador Prolog devolverÃ¡ V, o valor lÃ³gico de F por qualquer
valoraÃ§Ã£o v que atribui a cada sÃ­mbolo proposicional da lista S o valor lÃ³gico que ocorre na posiÃ§Ã£o correspondente na lista L*/
calc_valor(F,L1,L2,V) :- enesimo(N,L1,F), enesimo(N,L2,V).
calc_valor(neg X,L1,L2,0) :- calc_valor(X,L1,L2,1).
calc_valor(neg X,L1,L2,1) :- calc_valor(X,L1,L2,0).
calc_valor(X e Y, L1,L2,1) :- calc_valor(X,L1,L2,1),calc_valor(Y,L1,L2,1).
calc_valor(X e Y, L1,L2,0) :- not(calc_valor(X e Y,L1,L2,1)).
calc_valor(X ou Y, L1,L2,0) :- calc_valor(X,L1,L2,0),calc_valor(Y,L1,L2,0).
calc_valor(X ou Y, L1,L2,1) :- not(calc_valor(X ou Y,L1,L2,0)).
calc_valor(X imp Y,L1,L2,0) :- calc_valor(X,L1,L2,1), calc_valor(Y,L1,L2,0).
calc_valor(X imp Y,L1,L2,1) :- not(calc_valor(X imp Y,L1,L2,0)).


/* lista_n_0s_e_1s/2 Ã© tal que lista_n_0s_e_1s(N,L) Ã© verdadeiro se L Ã© uma lista constituÃ­da apenas por 0s e 1s de comprimento N, sendo N um nÃºmero inteiro nÃ£o negativo*/
lista_n_0s_e_1s(0,[]).
lista_n_0s_e_1s(N,[0|L]) :- N>0, N1 is N-1, lista_n_0s_e_1s(N1,L).
lista_n_0s_e_1s(N,[1|L]) :- N>0, N1 is N-1, lista_n_0s_e_1s(N1,L).


/*todas_listas_0s_1s/2 Ã© tal que todas_listas_0s_1s(N,L) recebe um nÃºmero inteiro nÃ£o negativo N e devolve a lista L constituida por todas as listas de comprimento N constituidas por Os e 1s.*/
todas_listas_0s_1s(N,R) :- findall(L,lista_n_0s_e_1s(N,L),R).

/* simb_prop/2 Ã© tal que sim_prop(Y,Y) Ã© verdadeiro se Y Ã© simbolo proposicional */
simb_prop(F,F) :- not(F = neg X), not(F = X e Y), not(F = X ou Y), not(F = X imp Y).
simb_prop(neg F, Z) :- simb_prop(F,Z).
simb_prop(X e _,U) :- simb_prop(X,U).
simb_prop(_ e Y,I) :- simb_prop(Y,I).
simb_prop(X ou _,U) :- simb_prop(X,U).
simb_prop(_ ou Y,I) :- simb_prop(Y,I).
simb_prop(X imp _,U) :- simb_prop(X,U).
simb_prop(_ imp Y,I) :- simb_prop(Y,I).


/*simbolos_formula/2 Ã© tal que simbolos_formula(F,L) Ã© verdadeiro se L Ã© a lista com todos os simbolos proposicionais da formula F*/
simbolos_formula(F,L) :- findall(U,simb_prop(F,U),T), eliminarep(T,L).

/*simbolos_conjunto/2 Ã© tal que simbolos_conjunto(L1,L2) Ã© verdadeiro se L2 Ã© a lista de todos os simbolos proposicionais que ocorrem nalguma formula da lista de formulas L1*/
simbolos_conjunto([],[]).
simbolos_conjunto([F|R],L) :- simbolos_formula(F,T), simbolos_conjunto(R,U), concatena(T,U,Y), eliminarep(Y,L).

/*todas_valoracoes_satisfazem/2 Ã© tal que todas_valoracoes_satisfazem(F,V) Ã© verdadeiro se V Ã© a lista de todas as valoracoes que satisfazem a formula F*/
todas_valoracoes_satisfazem(F,V) :- simbolos_formula(F,L), comprimento(L,N), todas_listas_0s_1s(N,R), findall(A,valoracao_satisfaz(F,L,R,A),V).

/*valoracao_satisfaz/4 Ã© tal que valoracao_satisfaz(F,S,L,V) Ã© verdadeiro se V Ã© uma valoracao de L que satisfaz a formula F, sendo S a lista de simbolos proposicionais de F.*/
valoracao_satisfaz(F,S,[X|_],X) :- calc_valor(F,S,X,1).
valoracao_satisfaz(F,S,[_|T],R) :- valoracao_satisfaz(F,S,T,R).


/*juntar_conjunto/2 Ã© tal que juntar_conjunto(L,V) Ã© verdadeiro se V Ã© a formula que se obtem de concatenar todas as formulas da lista L com eÂ´s */
juntar_conjunto([X|[]],X).
juntar_conjunto([H|R],P) :- juntar_conjunto(R,T),P= H e T.

/*imprime_valoracoes/3 Ã© tal que imprime_valoracoes(L,L,X) recebe uma lista L de simbolos proposicionais de uma dada formula e uma lista X com todas as valoracoes que satisfazem
a mesma formula, e imprime as valoracoes correspondentes a cada simbolo proposicional que permite satisfazer a formula.*/
imprime_valoracoes(L,L,[]).
imprime_valoracoes([],[],[]).
imprime_valoracoes(L,[X|R],[[V1|V2]|O]) :- write("v("), write(X), write(") = "), write(V1), (not(V2=[]) -> write(" e "),imprime_valoracoes(L,R,[V2|O]); (not(O=[]) -> nl, write("ou "), imprime_valoracoes(L,L,O);imprime_valoracoes(L,L,O))).


/* ************** Exercicio 1 ************** */
/*exercicio1/1 Ã© tal que exercicio1(L) recebe um conjunto de formulas L e devolve a informacao de todas as valoracoes que satisfazem esse conjunto*/
/*Baseamos o nosso raciocinio no facto de uma valoraÃ§Ã£o satisfazer um conjunto de fÃ³rmulas ser equivalente a essa valoraÃ§Ã£o satisfazer a fÃ³rmula obtida de juntar todas as fÃ³rmulas do conjunto com eÂ´s
Logo, a funÃ§ao simbolos_conjunto descobrirÃ¡ os simbolos proposicionais presentes no conjunto de fÃ³rmulas, para o imprimir das valoraÃ§Ãµes, juntar_conjunto juntarÃ¡ todas as fÃ³rmulas do conjuntos usando eÂ´s , de modo a obter uma fÃ³rmula
E obtemos as valoracoes que satisfazem a fÃ³rmula mencionada anteriormente, e Ã© mostrado ao utilizador as valoraÃ§Ãµes que satisfazem o conjunto, ou o facto de nÃ£o haver valoraÃ§Ãµes que satisfazem o conjunto*/
exercicio1(L) :- simbolos_conjunto(L,C), write("O conjunto de simbolos proposicionais do conjunto de formulas "), write(L), write(" é: "), write(C),nl, juntar_conjunto(L,F), write("As formulas do conjunto são satisfeitas, por qualquer valoração v, tal que: "),nl, todas_valoracoes_satisfazem(F,V), (V=[] -> write("Não existe nenhuma valoração que satisfaça todas as formulas do conjunto: "), write(L); imprime_valoracoes(C,C,V)),!.

/*Exemplos de objetivos que podem ser executados para testar o programa:
 - existem valoracoes que satisfazem: exercicio1([p imp (neg r imp q),r ou (p e q)]).
 - nao existem valoracoes que satisfazem: exercicio1([(p ou q) imp neg r, q e r]).
 */

/* -------------------------------------------------------*/

/*elimina/3 Ã© tal que elimina(X,L1,L2) Ã© verdadeiro se L2 Ã© a lista que resulta de retirar da lista L1 todas as ocorrÃªncias do elemento X*/
elimina(_,[],[]).
elimina(X,[X|L],L1) :- elimina(X,L,L1).
elimina(X,[Y|L],[Y|L1]) :- not(Y=X), elimina(X,L,L1).

/*elimina_lista/3 Ã© tal que elimina_lista(X,Y,L) Ã© verdadeiro se L Ã© a lista que resulta de retirar da lista Y todas as ocorrÃªncias dos elementos da lista X*/
elimina_lista([],L,L).
elimina_lista([X|R],T,L) :- elimina(X,T,P), elimina_lista(R,P,L).



/* ************** Exercicio 2 ************** */
/*exercicio2/2 Ã© tal que exercicio2(L,F) Ã© verdadeiro se F Ã© consequencia semantica do conjunto de formulas L*/
/*Baseamos o nosso raciocinio no facto de Â´F ser consequÃªncia semÃ¢ntica de CÂ´ ser equivalente a Â´(todas as fÃ³rmulas de C concatenadas com eÂ´s) implica FÂ´ ser tautologia, temos 2 casos:
Se o conjunto de fÃ³rmulas Ã© vazio, verificamos se F Ã© uma tautologia, obtendo todas as valoraÃ§Ãµes que satisfazem F, comparando essa lista com a lista de todas as valoraÃ§Ãµes possÃ­veis para a fÃ³rmula, por meio de eliminaÃ§Ã£o,
Isto Ã©, usando o elimina_lista, vamos eliminar todas as valoraÃ§Ãµes que satisfazem F do conjunto de todas as valoraÃ§Ãµes possÃ­veis e, se a lista obtida for [], entÃ£o sabemos que Ã© tautologia e informamos o utilizador de tal
Se tal nÃ£o acontecer, o utilizador Ã© informado e tambÃ©m Ã© informado uma valoraÃ§Ã£o que nÃ£o satisfaz, daÃ­ o elimina_lista, o resultado desta clausula Ã© a lista de valoraÃ§Ãµes que prova que F nÃ£o Ã© tautologia, e assim damos o exemplo ao utilizador.
Se o conjunto nÃ£o for vazio, o raciocinio Ã© o mesmo, sÃ³ que junta-se as fÃ³rmulas do conjunto com eÂ´s ,  graÃ§as Ã  clausula juntar_conjunto, e juntamos a fÃ³rmula resultante com a fÃ³rmula que queremos ver se Ã© consequÃªncia semÃ¢ntica
Com um implica, como a propriedade diz, e de seguida o procedimento Ã© idÃªntico ao indicado anteriormente, para a fÃ³rmula J= V imp F.*/
exercicio2([],F) :- todas_valoracoes_satisfazem(F,T), simbolos_formula(F,Q), comprimento(Q,N), todas_listas_0s_1s(N,E), elimina_lista(T,E,O), (O=[] -> write("'"), write(F), write("' e tautologia."), nl;  write("'"), write(F), write("' nao e tautologia."), nl, write("Uma valoracao que nao verifica este facto e, por exemplo: "), imprime_valoracoes(Q,Q,O)), !.
exercicio2(L,F) :- juntar_conjunto(L,V), J= V imp F, todas_valoracoes_satisfazem(J,T), simbolos_formula(J,Q), comprimento(Q,N), todas_listas_0s_1s(N,E), elimina_lista(T,E,O), (O=[] -> write("'"), write(F), write("' e consequencia semantica de "), write(L), nl; write("'"), write(F), write("' nao e consequencia semantica de "), write(L),nl, write("Uma valoracao que nao verifica este facto e, por exemplo: "), O= [B|_], imprime_valoracoes(Q,Q,[B])), !.

/*Exemplos de objetivos que podem ser executados para testar o programa:
 - e consequencia semantica: exercicio2([p imp (r imp q), r ou q], p imp q).
 - nao e consequencia semantica: exercicio2([p imp (r imp q), r ou q], q).
 */


/* -------------------------------------------------------*/

/*junta_elem_listaconj/3 Ã© um predicado auxiliar ao predicado partes*/
junta_elem_listaconj(_,[],[]).
junta_elem_listaconj(E,[X|R],[[E|X]|S]) :- junta_elem_listaconj(E,R,S).

/*partes/2 Ã© tal que partes(L,P) Ã© verdadeiro se P Ã© o conjunto das partes do conjunto L*/
partes([],[[]]).
partes([X|R],P) :- partes(R,S), junta_elem_listaconj(X,S,T), concatena(S,T,P).

/*conseq_semantica/2 Ã© tal que conseq_semantica(L,F) se F Ã© consequencia semantica de L
O raciocinio usado aqui Ã© o raciocinio usado no exercicio2, sÃ³ que jÃ¡ nÃ£o Ã© utilizado o elimina_lista pois esta clausula jÃ¡ nÃ£o informarÃ¡ o utilizador de nada, Ã© uma clausula auxiliar ao todas_conseq_semantica
Por isso, simplesmente compara-se a lista de valoraÃ§Ãµes que satisfazem o facto de F ser consequÃªncia semÃ¢ntica de L com todas as valoraÃ§Ãµes possÃ­veis, se as listas sÃ£o iguais, entÃ£o F Ã© consequÃªncia semÃ¢ntica de L*/
conseq_semantica([],F) :- todas_valoracoes_satisfazem(F,T), simbolos_formula(F,Q),comprimento(Q,N), todas_listas_0s_1s(N,E), T=E,!.
conseq_semantica(L,F) :- juntar_conjunto(L,V), J= V imp F, todas_valoracoes_satisfazem(J,T), simbolos_formula(J,Q),comprimento(Q,N), todas_listas_0s_1s(N,E),T=E,!.

/*lista_conseq_semanticas/3 Ã© tal que lista_conseq_semanticas(C,F,L) Ã© verdadeiro se L Ã© a lista de todos os subconjuntos de C tal que F Ã© consequencia semantica dos subconjuntos*/
lista_conseq_semanticas(C,F,V) :- partes(C,P), todas_conseq_semantica(P,F,V).

/*todas_conseq_semantica/3 Ã© tal que todas_conseq_semantica(P,F,L) Ã© verdadeiro se L Ã© a lista de todos os conjuntos de P dos quais F Ã© consequencia semantica*/
todas_conseq_semantica([],_,[]).
todas_conseq_semantica([X|R], F, [X|T]) :- conseq_semantica(X,F), todas_conseq_semantica(R,F,T).
todas_conseq_semantica([_|R],F,T) :- todas_conseq_semantica(R,F,T).

/*membro_listas/3 Ã© tal que membro_listas(L,T,X), Ã© verdadeiro se X Ã© uma lista da lista T tal que todos os elementos da lista L sÃ£o membros da lista X
Esta clausula serÃ¡ util para saber os conjuntos minimais pois se esta clausula Ã© verdade, todos os membros de L estÃ£o na lista X da lista T e procedemos a eliminar a lista X, como se pode ver na clausula minimais_aux*/
membro_listas([],_,_).
membro_listas([X|R],[Y|L],Y) :- membro(X,Y), membro_listas(R,[Y|L],Y).
membro_listas([X|R],[_|L],Y) :- membro_listas([X|R],L,Y).

/*minimais_aux/2 Ã© tal que minimais_aux(L,R) Ã© verdadeiro se R Ã© a lista que contÃ©m todos os conjuntos minimais de L, com repetidos*/
minimais_aux([X|[]],[X]).
minimais_aux([X|R],[X|L]) :- membro_listas(X,R,Y),!, elimina(Y,R,T), minimais_aux([X|T],L).
minimais_aux([_|R],L) :- minimais_aux(R,L).

/*minimais/3 Ã© tal que minimais(C,F,L) Ã© verdadeiro se L Ã© a lista dos subconjuntos minimais de C tal que F Ã© consequencia semantica dos subconjuntos*/
minimais(C,F,L) :- lista_conseq_semanticas(C,F,T), minimais_aux(T,R), eliminarep(R,L).


/* ************** Exercicio 3 ************** */
/*exercicio3/2 Ã© tal que Ã© imprimida a informacao do conjunto de todos os subconjuntos minimais do conjunto de formulas C dos quais a formula F Ã© conquencia semantica*/
exercicio3(C,F) :- (minimais(C,F,L) -> write("O conjunto de todos os subconjuntos minimais de "), write(C), write(" dos quais '"), write(F), write("' e consequencia semantica e: "), nl, write(L); write("Nao existe nenhum subconjunto de "), write(C), write(" que tenha como consequencia semantica a formula '"), write(F), write("'.")), !.

/*Exemplos de objetivos que podem ser executados para testar o programa:
 - existem subconjuntos como conquencia semantica: exercicio3([p imp q, p ou q,p,q],q).
 - nao existem subconjuntos como conquencia semantica: exercicio3([p imp q, p ou q,q],p e q).
 */
